*-----------------------------------------------------------------------------
* <Rating>-40</Rating>
*-----------------------------------------------------------------------------
    SUBROUTINE ATI.DS.JOIN.DEPOSITO(Y.VALUE)
*-----------------------------------------------------------------------------
* Developer Name     : ATI Yudistia Adnan
* Development Date   : 20161021
* Description        : Routine to print join holder deposits
*-----------------------------------------------------------------------------
* Modification History:-
*-----------------------------------------------------------------------------
* Date               :
* Modified by        :
* Description        :
*-----------------------------------------------------------------------------
    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.MM.MONEY.MARKET
    $INSERT I_F.VERSION
    $INSERT I_F.CUSTOMER
    $INSERT I_F.DEAL.SLIP.FORMAT

*-----------------------------------------------------------------------------
MAIN:
*-----------------------------------------------------------------------------
    GOSUB INIT
    GOSUB PROCESS

    RETURN

*-----------------------------------------------------------------------------
INIT:
*-----------------------------------------------------------------------------
    FN.MM.MONEY.MARKET = 'F.MM.MONEY.MARKET'
    F.MM.MONEY.MARKET  = ''
    CALL OPF(FN.MM.MONEY.MARKET, F.MM.MONEY.MARKET)

    FN.CUSTOMER = "F.CUSTOMER"
    F.CUSTOMER  = ""
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)

    FN.DEAL.SLIP.FORMAT = "F.DEAL.SLIP.FORMAT"
    F.DEAL.SLIP.FORMAT  = ""
    CALL OPF(FN.DEAL.SLIP.FORMAT, F.DEAL.SLIP.FORMAT)

    Y.APP      = 'MM.MONEY.MARKET'
    Y.FLD.NAME = 'ATI.JOINT.NAME':VM:'ATI.MM.JOIN.TYP':VM:'MUD.PRINTED.NAM'
    Y.POS      = ''
    CALL MULTI.GET.LOC.REF(Y.APP,Y.FLD.NAME,Y.POS)
    Y.ATI.JOINT.NAME.POS  = Y.POS<1,1>
    Y.ATI.MM.JOIN.TYP.POS = Y.POS<1,2>
    Y.MM.MUD.PRINTED.NAM.POS = Y.POS<1,3>

    RETURN

*-----------------------------------------------------------------------------
PROCESS:
*-----------------------------------------------------------------------------
    Y.DEAL.SLIP = R.VERSION(EB.VER.D.SLIP.FORMAT)

    CALL F.READ(FN.DEAL.SLIP.FORMAT, Y.DEAL.SLIP, R.DEAL.SLIP.FORMAT, F.DEAL.SLIP.FORMAT, DEAL.SLIP.FORMAT.ERR)
    Y.FORMAT = R.DEAL.SLIP.FORMAT<EB.DSF.FORMAT>

    LOCATE "@ATI.DS.JOIN.DEPOSITO" IN Y.FORMAT<1,1> SETTING Y.POS THEN
        Y.REAL.POSITION     = R.DEAL.SLIP.FORMAT<EB.DSF.COLUMN, Y.POS>
        Y.REAL.POSITION.FLD = FIELD(Y.REAL.POSITION, ",", 1)
        Y.POS += 1
        Y.POSITION = R.DEAL.SLIP.FORMAT<EB.DSF.COLUMN, Y.POS>

        Y.GET.NUM.DIVIDE = (FIELD(Y.POSITION, ",", 1) - FIELD(Y.REAL.POSITION, ",", 1)) - 1
        Y.CONV.REAL.POS = FIELD(Y.REAL.POSITION, ",", 1) - 1
        Y.CONV.POS = FIELD(Y.POSITION, ",", 1)
    END

    Y.MM.ID = Y.VALUE
    CALL F.READ(FN.MM.MONEY.MARKET, Y.MM.ID, R.MM.MONEY.MARKET, F.MM.MONEY.MARKET, MM.ERR)
    Y.JOINT.NAME = R.MM.MONEY.MARKET<MM.LOCAL.REF,Y.ATI.JOINT.NAME.POS>

    IF Y.JOINT.NAME THEN

        CONVERT SM TO " " IN Y.JOINT.NAME

        Y.ROW.DATA = FMT(Y.JOINT.NAME, '50T')

        CONVERT @TM TO FM IN Y.ROW.DATA

        Y.ROW.CNT = DCOUNT(Y.ROW.DATA, FM)

        FOR Y.LOOP = 1 TO Y.ROW.CNT
            IF Y.FIX.JOINT.NAME EQ "" THEN
                Y.FIX.JOINT.NAME<-1> = FMT(Y.ROW.DATA<Y.LOOP>, Y.CONV.POS:"L")
            END
            ELSE
                Y.FIX.JOINT.NAME<-1> = FMT("", Y.CONV.REAL.POS:"R") : Y.ROW.DATA<Y.LOOP>
            END
        NEXT Y.LOOP

        CONVERT FM TO CHAR(10) IN Y.FIX.JOINT.NAME
        Y.VALUE = Y.FIX.JOINT.NAME
    END ELSE
        Y.VALUE = R.MM.MONEY.MARKET<MM.LOCAL.REF,Y.MM.MUD.PRINTED.NAM.POS>
    END


    RETURN
*-----------------------------------------------------------------------------
END












