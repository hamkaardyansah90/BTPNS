*-----------------------------------------------------------------------------
* <Rating>-182</Rating>
* 17:57:49 03 OCT 2016 * 16:18:41 03 OCT 2016 * 11:33:57 06 SEP 2016 * 11:23:33 06 SEP 2016 * 11:19:30 06 SEP 2016 * 11:17:48 06 SEP 2016 * 11:12:27 06 SEP 2016 * 15:02:45 05 SEP 2016 * 13:01:03 29 AUG 2016 * 12:52:59 29 AUG 2016
* CBS-APP1-JKT/t24sit * CBS-APP1-JKT/t24sit * CBS-APP1-JKT/t24sit * CBS-APP1-JKT/t24sit * CBS-APP1-JKT/t24sit * CBS-APP1-JKT/t24sit * CBS-APP1-JKT/t24sit * CBS-APP1-JKT/t24sit * WIN-KVUAVRB60BE/R14 * WIN-KVUAVRB60BE/R14
*-----------------------------------------------------------------------------
    SUBROUTINE ATI.BM.EXT.REPORT.SELECT
*-----------------------------------------------------------------------------
* Developer Name    : Dwi K
* Development Date  : 20150917
* Description       : Extract Report
*-----------------------------------------------------------------------------
* Modification History:-
*-----------------------------------------------------------------------------
* Date               : 20160620
* Modified by        : Dwi K
* Description        : Add function to splitted data
*-----------------------------------------------------------------------------
* Modification History:-
*-----------------------------------------------------------------------------
* Date               : 20161103
* Modified by        : Fatkhur Rohman
* Description        : Change logic for var common extractor Bank DATE
*                      Read from DATES table, do not use R.DATES from I_COMMON
*-----------------------------------------------------------------------------
* Modification History:-
*-----------------------------------------------------------------------------
* Date               : 20161110
* Modified by        : Fatkhur Rohman
* Description        : Change logic for var common XXX.TIME.DATE
*                      Read from DATES table, not SYSTEM.DATE
*                      BTPNS - D5553
*-----------------------------------------------------------------------------

    $INSERT I_EQUATE
    $INSERT I_COMMON
    $INSERT I_F.LOCKING
    $INSERT I_BATCH.FILES
    $INSERT I_TSA.COMMON
    $INSERT I_F.ATI.TH.EXT.REPORT
    $INSERT I_F.DATES
    $INSERT I_ATI.BM.EXT.REPORT.COMMON

*-----------------------------------------------------------------------------
MAIN:
*-----------------------------------------------------------------------------

    GOSUB INIT

    RETURN

*-----------------------------------------------------------------------------
INIT:
*-----------------------------------------------------------------------------
    CONVERT SM TO FM IN Y.BATCH.DETAILS

    PROCESS.GOAHEAD = ""
    IF CONTROL.LIST EQ "" THEN
        CONTROL.LIST = Y.BATCH.DETAILS :FM: "FINAL.WRITE"
    END
    Y.CONTROL.LIST.FUNCTION = CONTROL.LIST<1,1>

    FN.LOC.DATE = 'F.DATES'
    F.LOC.DATE  = ''
    CALL OPF(FN.LOC.DATE,F.LOC.DATE)

    Y.COMPANY.ID = ID.COMPANY
    CALL F.READ(FN.LOC.DATE,Y.COMPANY.ID,R.LOC.DATE,F.LOC.DATE,LOC.DATE.ERR)

    LIST.PARAM = ""
    SEL.CMD    = ""

    Y.SYS.DATE      = ''
    Y.SYS.DATE      = OCONV(DATE(),"D-")
    Y.LAST.SYS.DATE = Y.SYS.DATE[7,4]:Y.SYS.DATE[1,2]:Y.SYS.DATE[4,2]
    Y.DAYS          = '-01C'
    CALL CDT('', Y.LAST.SYS.DATE, Y.DAYS)
    Y.SYS.TIME      = OCONV(TIME(), "MTS")

    Y.SYS.DATE.VAL           = Y.SYS.DATE[7,4]:Y.SYS.DATE[1,2]:Y.SYS.DATE[4,2]
* Y.SYS.TIME.DATE.VAL      = Y.SYS.DATE[7,4]:Y.SYS.DATE[1,2]:Y.SYS.DATE[4,2]:Y.SYS.TIME[1,2]:Y.SYS.TIME[4,2]
*Y.SYS.TIME.DATE.VAL      = Y.SYS.DATE[9,2]:Y.SYS.DATE[1,2]:Y.SYS.DATE[4,2]:'0000'
    Y.SYS.TIME.DATE.VAL      = R.LOC.DATE<EB.DAT.TODAY>[6]:'0000'
*Y.LAST.SYS.DATE.VAL      = Y.SYS.DATE[7,4]:Y.SYS.DATE[1,2]:Y.LAST.SYS.DATE
    Y.LAST.SYS.DATE.VAL      = Y.LAST.SYS.DATE
* Y.LAST.SYS.TIME.DATE.VAL = Y.SYS.DATE[7,4]:Y.SYS.DATE[1,2]:Y.LAST.SYS.DATE:Y.SYS.TIME[1,2]:Y.SYS.TIME[4,2]
*Y.LAST.SYS.TIME.DATE.VAL = Y.LAST.SYS.DATE[3,6]:'0000'
    Y.LAST.SYS.TIME.DATE.VAL = R.LOC.DATE<EB.DAT.LAST.WORKING.DAY>[6]:'0000'

    BEGIN CASE
    CASE Y.CONTROL.LIST.FUNCTION EQ "FINAL.WRITE"
        GOSUB FINAL.WRITE
    CASE OTHERWISE
        GOSUB PROCESS
    END CASE

    RETURN
*-----------------------------------------------------------------------------
PROCESS:
*-----------------------------------------------------------------------------
    Y.ID.EXT.REP = CONTROL.LIST<1,1>
    GOSUB GET.DATA.EXT.REP

    BEGIN CASE
    CASE Y.TABLES.TYPE EQ "" OR Y.TABLES.TYPE EQ "LIVE"
        FN.APP = 'F.':Y.APP
        CALL OPF(FN.APP,F.APP)
    CASE Y.TABLES.TYPE EQ "$HIS"
        FN.APP = 'F.':Y.APP:'$HIS'
        CALL OPF(FN.APP,F.APP)
    CASE Y.TABLES.TYPE EQ "$NAU"
        FN.APP = 'F.':Y.APP:'$NAU'
        CALL OPF(FN.APP,F.APP)
    END CASE

*    CHANGE '!TODAY' TO R.DATES(EB.DAT.TODAY) IN Y.FOLDER
*    CHANGE '!LAST.WORKING.DAY' TO R.DATES(EB.DAT.LAST.WORKING.DAY) IN Y.FOLDER
*    CHANGE '!NEXT.WORKING.DAY' TO R.DATES(EB.DAT.NEXT.WORKING.DAY) IN Y.FOLDER
    CHANGE '!TODAY' TO R.LOC.DATE<EB.DAT.TODAY> IN Y.FOLDER
    CHANGE '!LAST.WORKING.DAY' TO R.LOC.DATE<EB.DAT.LAST.WORKING.DAY> IN Y.FOLDER
    CHANGE '!NEXT.WORKING.DAY' TO R.LOC.DATE<EB.DAT.NEXT.WORKING.DAY> IN Y.FOLDER
    CHANGE '!USER' TO OPERATOR IN Y.FOLDER
    CHANGE '!LOCAL.CCY' TO LCCY IN Y.FOLDER
    CHANGE '!LANGUAGE' TO LNGG IN Y.FOLDER
    CHANGE '!COMPANY' TO ID.COMPANY IN Y.FOLDER
    CHANGE '!SYSTEM.DATE' TO Y.SYS.DATE.VAL IN Y.FOLDER
    CHANGE '!SERVER.NAME' TO SYSTEM(50) IN Y.FOLDER

    FN.FOLDER = Y.FOLDER
*    CALL OPF(FN.FOLDER,F.FOLDER)

    OPEN FN.FOLDER TO F.FOLDER ELSE
        Y.EXEC.CMD = 'CREATE.FILE ':FN.FOLDER:' TYPE=UD'
        EXECUTE Y.EXEC.CMD
        OPEN FN.FOLDER TO F.FOLDER ELSE
            RETURN
        END
    END

    GOSUB DELETE.FILE

*------SELECTION------
    IF Y.SEL.RTN THEN
        CALL @Y.SEL.RTN(SEL.LIST)
        CALL BATCH.BUILD.LIST("",SEL.LIST)
    END
    ELSE
        Y.CNT.SEL.FLD = DCOUNT(Y.SEL.FLD,VM)
        FOR Y.FLD = 1 TO Y.CNT.SEL.FLD
            SEL.CMD := " ":Y.SEL.FLD<1,Y.FLD>
            SEL.CMD := " ":Y.SEL.CRIT<1,Y.FLD>

            Y.CUR.SEL.VAL = Y.SEL.VAL<1,Y.FLD>
*            CHANGE '!TODAY' TO R.DATES(EB.DAT.TODAY) IN Y.CUR.SEL.VAL
*            CHANGE '!LAST.WORKING.DAY' TO R.DATES(EB.DAT.LAST.WORKING.DAY) IN Y.CUR.SEL.VAL
*            CHANGE '!NEXT.WORKING.DAY' TO R.DATES(EB.DAT.NEXT.WORKING.DAY) IN Y.CUR.SEL.VAL
            CHANGE '!TODAY' TO R.LOC.DATE<EB.DAT.TODAY> IN Y.CUR.SEL.VAL
            CHANGE '!LAST.WORKING.DAY' TO R.LOC.DATE<EB.DAT.LAST.WORKING.DAY> IN Y.CUR.SEL.VAL
            CHANGE '!NEXT.WORKING.DAY' TO R.LOC.DATE<EB.DAT.NEXT.WORKING.DAY> IN Y.CUR.SEL.VAL
            CHANGE '!SYSTEM.DATE' TO Y.SYS.DATE.VAL IN Y.CUR.SEL.VAL
            CHANGE '!SYSTEM.TIME.DATE' TO Y.SYS.TIME.DATE.VAL IN Y.CUR.SEL.VAL
            CHANGE '!LAST.SYSTEM.DATE' TO Y.LAST.SYS.DATE.VAL IN Y.CUR.SEL.VAL
            CHANGE '!LAST.SYSTEM.TIME.DATE' TO Y.LAST.SYS.TIME.DATE.VAL IN Y.CUR.SEL.VAL

            SEL.CMD := " ":Y.CUR.SEL.VAL
            SEL.CMD := " ":Y.SEL.COMB<1,Y.FLD>
        NEXT Y.FLD

        LIST.PARAM<1> = ""
        LIST.PARAM<2> = FN.APP
        LIST.PARAM<3> = SEL.CMD
        LIST.PARAM<6> = ""
        LIST.PARAM<7> = ""

        CALL BATCH.BUILD.LIST(LIST.PARAM,"")
    END

    RETURN

*-----------------------------------------------------------------------------
FINAL.WRITE:
*-----------------------------------------------------------------------------
    Y.CNT.BATCH.DATA = DCOUNT(Y.BATCH.DETAILS,FM)
    FOR YLOOP = 1 TO Y.CNT.BATCH.DATA
        Y.ID.EXT.REP = Y.BATCH.DETAILS<YLOOP>
        GOSUB GET.DATA.EXT.REP

        FN.FOLDER = Y.FOLDER
        F.FOLDER  = ''
*        CALL OPF(FN.FOLDER,F.FOLDER)

        OPEN FN.FOLDER TO F.FOLDER ELSE
            Y.EXEC.CMD = 'CREATE.FILE ':FN.FOLDER:' TYPE=UD'
            EXECUTE Y.EXEC.CMD
            OPEN FN.FOLDER TO F.FOLDER ELSE
                RETURN
            END
        END

        FN.EXT.TEMP = 'F.ATI.EXT.REP.':Y.ID.EXT.REP:'.TEMP'
        F.EXT.TEMP  = ''
        CALL OPF(FN.EXT.TEMP,F.EXT.TEMP)

        FN.FILE.EXT = 'F.ATI.TT.FILE.EXT.REPORT'
        F.FILE.EXT  = ''
        CALL OPF(FN.FILE.EXT,F.FILE.EXT)

        Y.EXT.TEMP.CMD = "SELECT ":FN.EXT.TEMP
        CALL EB.READLIST(Y.EXT.TEMP.CMD,Y.EXT.TEMP.LIST,"",Y.EXT.TEMP.CNT,Y.EXT.TEMP.ERR)

        FOR I = 1 TO Y.EXT.TEMP.CNT
            Y.AGENT.NO = FIELD(Y.EXT.TEMP.LIST<I>,'-',1)
            CHANGE '!AGENT.NUMBER' TO Y.AGENT.NO IN Y.FILENAME

            IF Y.SPLIT.FILE EQ 'YES' THEN
                Y.FILENAME.NEW = Y.FILENAME:'_':Y.EXT.TEMP.LIST<I>
                GOSUB WRITE.TEXTFILE
            END ELSE
                Y.FILENAME.NEW = Y.FILENAME
                GOSUB WRITE.TEXTFILE
            END

            CALL F.READ(FN.FILE.EXT,Y.ID.EXT.REP,R.FILE.EXT,F.FILE.EXT,FILE.EXT.ERR)
            FIND Y.FILENAME.NEW IN R.FILE.EXT SETTING POSF.FILE ELSE
                R.FILE.EXT<-1> = Y.FILENAME.NEW
            END
            CALL F.WRITE(FN.FILE.EXT,Y.ID.EXT.REP,R.FILE.EXT)

        NEXT I

    NEXT YLOOP

    RETURN
*-----------------------------------------------------------------------------
WRITE.TEXTFILE:
*-----------------------------------------------------------------------------
    OPENSEQ FN.FOLDER, Y.FILENAME.NEW TO F.FOLDER ELSE
        CREATE F.FOLDER THEN
        END
    END

    CALL F.READ(FN.EXT.TEMP,Y.EXT.TEMP.LIST<I>,R.EXT.TEMP,F.EXT.TEMP,EXT.TEMP.ERR)
    Y.CNT.REC.DATA = DCOUNT(R.EXT.TEMP,FM)

    FOR YLOOP3 = 1 TO Y.CNT.REC.DATA
        WRITESEQ R.EXT.TEMP<YLOOP3> APPEND TO F.FOLDER ELSE
            CRT 'FILE WRITE ERROR'
            STOP
        END
    NEXT YLOOP3

    WEOFSEQ F.FOLDER
    CLOSESEQ F.FOLDER

*   CALL F.READ(FN.FOLDER,Y.FILENAME.NEW,R.FOLDER,F.FOLDER,FOLDER.ERR)
*   R.FOLDER<-1> = R.EXT.TEMP
*   CALL F.WRITE(FN.FOLDER,Y.FILENAME.NEW,R.FOLDER)

    RETURN
*-----------------------------------------------------------------------------
GET.DATA.EXT.REP:
*-----------------------------------------------------------------------------
    CALL F.READ(FN.ID.REP,Y.ID.EXT.REP,R.ID.REP,F.ID.REP,REP.ERR)
    Y.APP          = R.ID.REP<EXT.REP.APPLICATION>
    Y.FOLDER       = R.ID.REP<EXT.REP.OUTPUT.DIR>
    Y.SEL.FLD      = R.ID.REP<EXT.REP.SEL.FLD>
    Y.SEL.CRIT     = R.ID.REP<EXT.REP.SEL.CRIT>
    Y.SEL.VAL      = R.ID.REP<EXT.REP.SEL.VALUE>
    Y.SEL.COMB     = R.ID.REP<EXT.REP.SEL.COMB>
    Y.SEL.RTN      = R.ID.REP<EXT.REP.SEL.RTN>
    Y.FILENAME     = R.ID.REP<EXT.REP.TARGET.FILE>
    Y.SPLIT.FILE   = R.ID.REP<EXT.REP.SPLIT.FILE>
    Y.TABLES.TYPE  = R.ID.REP<EXT.REP.TABLES.TYPE>

*    CHANGE '!TODAY' TO R.DATES(EB.DAT.TODAY) IN Y.FILENAME
*    CHANGE '!LAST.WORKING.DAY' TO R.DATES(EB.DAT.LAST.WORKING.DAY) IN Y.FILENAME
*    CHANGE '!NEXT.WORKING.DAY' TO R.DATES(EB.DAT.NEXT.WORKING.DAY) IN Y.FILENAME
    CHANGE '!TODAY' TO R.LOC.DATE<EB.DAT.TODAY> IN Y.FILENAME
    CHANGE '!LAST.WORKING.DAY' TO R.LOC.DATE<EB.DAT.LAST.WORKING.DAY> IN Y.FILENAME
    CHANGE '!NEXT.WORKING.DAY' TO R.LOC.DATE<EB.DAT.NEXT.WORKING.DAY> IN Y.FILENAME
    CHANGE '!USER' TO OPERATOR IN Y.FILENAME
    CHANGE '!LOCAL.CCY' TO LCCY IN Y.FILENAME
    CHANGE '!LANGUAGE' TO LNGG IN Y.FILENAME
    CHANGE '!COMPANY' TO ID.COMPANY IN Y.FILENAME

    CHANGE '!SYSTEM.DATE' TO Y.SYS.DATE.VAL IN Y.FILENAME
    CHANGE '!SERVER.NAME' TO SYSTEM(50) IN Y.FILENAME

*    CHANGE '!TODAY' TO R.DATES(EB.DAT.TODAY) IN Y.FOLDER
*    CHANGE '!LAST.WORKING.DAY' TO R.DATES(EB.DAT.LAST.WORKING.DAY) IN Y.FOLDER
*    CHANGE '!NEXT.WORKING.DAY' TO R.DATES(EB.DAT.NEXT.WORKING.DAY) IN Y.FOLDER
    CHANGE '!TODAY' TO R.LOC.DATE<EB.DAT.TODAY> IN Y.FOLDER
    CHANGE '!LAST.WORKING.DAY' TO R.LOC.DATE<EB.DAT.LAST.WORKING.DAY> IN Y.FOLDER
    CHANGE '!NEXT.WORKING.DAY' TO R.LOC.DATE<EB.DAT.NEXT.WORKING.DAY> IN Y.FOLDER
    CHANGE '!USER' TO OPERATOR IN Y.FOLDER
    CHANGE '!LOCAL.CCY' TO LCCY IN Y.FOLDER
    CHANGE '!LANGUAGE' TO LNGG IN Y.FOLDER
    CHANGE '!COMPANY' TO ID.COMPANY IN Y.FOLDER
    CHANGE '!SYSTEM.DATE' TO Y.SYS.DATE.VAL IN Y.FOLDER
    CHANGE '!SERVER.NAME' TO SYSTEM(50) IN Y.FOLDER

    RETURN
*-----------------------------------------------------------------------------
DELETE.FILE:
*-----------------------------------------------------------------------------
    FN.FILE.EXT = 'F.ATI.TT.FILE.EXT.REPORT'
    F.FILE.EXT  = ''
    CALL OPF(FN.FILE.EXT,F.FILE.EXT)

    CALL F.READ(FN.FILE.EXT,Y.ID.EXT.REP,R.FILE.EXT,F.FILE.EXT,FILE.EXT.ERR)
    Y.CNT.FILE = DCOUNT(R.FILE.EXT,FM)

    FOR YLOOP2 = 1 TO Y.CNT.FILE
        Y.CURRENT.FILE = R.FILE.EXT<YLOOP2>
        DELETE F.FOLDER, Y.CURRENT.FILE
    NEXT YLOOP2

    DELETE F.FILE.EXT,Y.ID.EXT.REP

    FN.EXT.TEMP = 'F.ATI.EXT.REP.':Y.ID.EXT.REP:'.TEMP'
    F.EXT.TEMP  = ''
    CALL OPF(FN.EXT.TEMP,F.EXT.TEMP)

    CLEARFILE F.EXT.TEMP

    RETURN
*-----------------------------------------------------------------------------
END















